#include <WiFi.h>
#include <WiFiUdp.h>

//replace with your Wi-Fi credentials --

const char* ssid = "ssid";
const char* password = "passwd";

// aet up UDP
WiFiUDP udp;
const IPAddress broadcastIP(192, 168, 1, 255);  // this can change 
const unsigned int udpPort = 4210;

// analog pin used for soil compaction sensing
const int sensorPin = 34;

// Timing for broadcasting
unsigned long lastBroadcast = 0;
const unsigned long interval = 5000;  // Send data every 5 seconds

void setupWiFi() {
  Serial.print("Connecting to Wi-Fi");
  WiFi.begin(ssid, password);
  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print("-");
  }
  Serial.println("\nWi-Fi connected");
  Serial.print("Local IP: ");
  Serial.println(WiFi.localIP());
}

void setup() {
  Serial.begin(115200);
  pinMode(sensorPin, INPUT);
  setupWiFi();
  udp.begin(udpPort);
}

void loop() {
  unsigned long currentMillis = millis();

  if (currentMillis - lastBroadcast >= interval) {
    lastBroadcast = currentMillis;
    int sensorValue = analogRead(sensorPin);
    float compactionLevel = map(sensorValue, 0, 4095, 0, 100);
    Serial.printf("soil compaction level: %.3f %%\n", compactionLevel);
    String message = "SENSOR_ID:001;COMPACTION:" + String(compactionLevel);

    // send message via UDP broadcast
    udp.beginPacket(broadcastIP, udpPort);
    udp.print(message);
    udp.endPacket();
  }
}
